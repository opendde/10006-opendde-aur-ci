# Maintainer: Marcel Korpel <marcel[dot]korpel[at]gmail>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>
# Contributor: Peter Richard Lewis <plewis@aur.archlinux.org>
# Contributor: Emiliano Vavassori <syntaxerrormmm@gmail.com>

pkgname=ruby-highline-1.6
_gemname=highline
pkgver=1.6.21
pkgrel=1
pkgdesc="A high-level text user interface toolkit for Ruby, version $pkgver"
arch=('any')
url="http://rubygems.org/gems/highline"
license=('GPL2' 'RUBY')
makedepends=('ruby-rdoc')
depends=('ruby')
provides=("ruby-highline=$pkgver")
conflicts=('ruby-highline')
options=(!emptydirs)
source=(http://rubygems.org/downloads/${_gemname}-${pkgver}.gem)
noextract=(${_gemname}-${pkgver}.gem)
sha256sums=('c136298eee86ceff87baadc71d764ea07986f89805636e4a6a305b2d5da07519')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_gemname-$pkgver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
